#compdef sub

local context state state_descr line
local ret=1
local -a list
local comps

_arguments -C \
           '1: :->cmds' \
           '*:: :->args' && ret=0

export LASTCOMPWORD="${line[-1]}"

# Special-case some sub-commands
case "${line[1]}" in
  cd)
    _files -W "$SOURCE_DIR" -/
    return 0
  ;;
esac

# Or use the commands themselves
case $state in
  cmds)
    comps="$(_call_program sub-cmds sub completions --commands)"
    ;;
  args)
    comps="$(_call_program sub-args sub completions $line[@])"
    ;;
esac


if [[ -z "$comps" ]]; then
  _message "No completions"
else
  list=("${(ps:\n:)comps}")

  _values "sub command" "${list[@]}"
fi

return ret
# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
