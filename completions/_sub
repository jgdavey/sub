#compdef sub

local context state state_descr line
local ret=1
local -a list
local comps

_arguments -C \
           '1: :->cmds' \
           '*:: :->args' && ret=0

export COMP_WORD="${line[-1]}"

# Special-case some sub-commands
case "${line[1]}" in
  cd)
    _files -W "$SOURCE_DIR" -/
    return 0
  ;;
esac

# Or use the commands themselves
case $state in
  cmds)
    comps="$(_call_program sub-cmds sub completions --commands)"
    ;;
  args)
    comps="$(_call_program sub-args sub completions $line[@])"
    has="$?"
    ;;
esac

if [[ -z "$comps" ]]; then
  if [[ "$has" = 42 ]]; then
    _default # fallback to generic (usually filename) completion
  else
    ret=1
    _message "No completions"
  fi
else
  ret=0
  list=("${(ps:\n:)comps}")

  _values "sub-command" "${list[@]}"
fi

unset COMP_WORD

return ret
# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
